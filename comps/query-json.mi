<%flags>
extends => undef;
</%flags>

<%class>
  has '_id';
  has '_col';
</%class>

<%init>
  my ($obj, $objcolumns) = Sentosa::Objects::get_recordSource({
      id => $._id,
      type => 'form',
      userid => $m->session->{auth_id}
    });
  if (!$obj) { $m->not_found(); }; # form not found

  my ($opt) = map { $_->{options} } grep { ($_->{type} eq 'select2') && ($_->{col} eq $._col) } @{$objcolumns};

  my $columns = [
    { col => $opt->{id},   search => $.args->{id}, searchcriteria => '='   },
    { col => $opt->{text}, search => $.args->{q},  searchcriteria => 'SUB' }
  ];

  my $h = DBI->connect($obj->{db}, $obj->{username}, $obj->{password}) or die("connection to ".$obj->{db}." error.\n");
  $h->{sqlite_unicode} = 1;
  $h->{mysql_enable_utf8} = 1;

  my $q = Sentosa::SQL::selectQuery({
    source => $opt->{source},
    columns => $columns,
    order => 'ASC',
    goto => {},
    start => undef,
    length => undef,
    dbms => $h->{Driver}->{Name}
  });
  my $query_limit    = $q->{'query_limit'};
</%init>
<%perl>
  #### get rows ####
  # { Slice => {} } get array of rows where each row is stored as a hash
  # http://search.cpan.org/~timb/DBI-1.634/DBI.pm#selectall_arrayref
  my @rows = 
    map {
      {
        'id'   => $_->{$opt->{id}},
        'text' => $_->{$opt->{text}}
      }
    } @{$h->selectall_arrayref($query_limit, { Slice => {} }, @{$q->{'query_limit_data'}})};

  my %j = (
    'results' => \@rows,
    'more' => 'false'
  );

  $m->send_json( \%j );
</%perl>